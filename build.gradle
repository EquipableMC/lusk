// some elements of this file are taken from skBee's build.gradle
// https://github.com/ShaneBeee/SkBee
import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'com.gradleup.shadow' version '8.3.0'
    id 'java'
}

version = "1.3"
def latestJava = 21
def oldestJava = 16

compileJava {options.encoding = "UTF-8"}

repositories {
    mavenCentral()
    maven { url 'https://repo.papermc.io/repository/maven-public/' }
    maven {
        name = 'sonatype'
        url = 'https://oss.sonatype.org/content/groups/public/'
    }
    maven {
        url 'https://repo.codemc.io/repository/maven-snapshots/'
    }
    maven {
        url 'https://repo.skriptlang.org/releases'
    }
    maven {
        url "https://oss.sonatype.org/content/repositories/snapshots/"
    }
    maven {
        url "https://jitpack.io/"
    }
}

dependencies {
    implementation 'org.jetbrains:annotations:20.1.0'
    compileOnly("io.papermc.paper:paper-api:1.21.3-R0.1-SNAPSHOT")
    compileOnly 'com.github.SkriptLang:Skript:2.9.5'
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    implementation 'org.bstats:bstats-base:3.1.0'
    implementation 'com.vdurmont:semver4j:3.1.0'
    implementation 'net.wesjd:anvilgui:1.10.3-SNAPSHOT'
    shadow 'junit:junit:3.8.2'
}

java {
    toolchain.languageVersion.set(JavaLanguageVersion.of(latestJava))
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs += ['-source', '' + oldestJava, '-target', '' + oldestJava]
}

processResources {
    filter ReplaceTokens, tokens: ["version": project.version]
}

shadowJar {
    archiveFileName = project.name + "-" + project.version + ".jar"
    dependencies {
        include(dependency('org.bstats:bstats-bukkit'))
        include(dependency('org.bstats:bstats-base'))
    }
    relocate 'org.bstats', 'it.jakegblp.lusk.libs.bstats'
    relocate 'net.wesjd.anvilgui', 'it.jakegblp.lusk.libs.anvilgui'
}